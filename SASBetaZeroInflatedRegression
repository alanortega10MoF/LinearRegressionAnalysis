*input data;
DATA phibe;
INPUT Percent Price Houses Age @@;
DATALINES;
0 455 69 21 80 308 223 15
0 316 244 24 75 159 84 13
36.4 210 236 31 0 147 54 37
50 557 183 16 44.4 704 199 18
33.3 232 73 6 0 593 119 38
50 626 230 20 20 738 156 8
27.3 343 60 14 55.6 256 206 34
80 246 201 17 85.7 345 38 22
42.9 631 217 11 50 450 158 7
0 630 222 42 0 491 239 27
71.4 356 85 22 28.6 441 103 15
25 481 240 16 88.9 212 222 18
50 181 197 42 50 574 56 35
20 264 235 19 33.3 647 138 35
87.5 297 88 17 0 630 18 60
;
RUN;

*normalize;
DATA phibe;
SET phibe;
Percent = Percent / 100;
Price = Price / 100;
Houses = Houses / 100;
RUN;

*fit the null model;
*betas predict zero;
*gammas predict the non-zero part;
PROC NLMIXED DATA= phibe;
PARMS 
b0=0.1 g0=0.1 phi=0.1;
pi0 = exp(b0) / (1 + exp(b0));
mu = exp(g0) / (1 + exp(g0));
IF (Percent = 0)
THEN loglikelihood = log(pi0);
ELSE loglikelihood = log(1-pi0) + lgamma(phi) - lgamma(mu*phi)
- lgamma((1-mu)*phi) + (mu*phi-1)*log(Percent)
+ ((1-mu)*phi-1)*log(1-Percent);
MODEL Percent ∼ GENERAL(loglikelihood);
RUN;

*fit the full model with all predictors;
*betas predict zero, choosing age;
*gammas predict the non-zero part, choosing price and houses;
PROC NLMIXED DATA= phibe;
PARMS 
b0=0.1 b1=0.1
g0=0.1 g1=0.1 g2=0.1
phi=0.1;
pi0 = exp(b0 + b1*age) / (1 + exp(b0 + b1*age));
mu = exp(g0 + g1*price + g2*houses) / (1 + exp(g0 + g1*price + g2*houses));
IF (Percent = 0) 
THEN loglikelihood = log(pi0);
ELSE loglikelihood = log(1-pi0) + lgamma(phi) - lgamma(mu*phi)
- lgamma((1-mu)*phi) + (mu*phi-1)*log(Percent)
+ ((1-mu)*phi-1)*log(1-Percent);
MODEL Percent ∼ GENERAL(loglikelihood);
RUN;

DATA dev;
deviance = 25.2 - 11.1;
pval = 1 - probchi(deviance, 5);
RUN;

PROC PRINT DATA= dev; RUN;
