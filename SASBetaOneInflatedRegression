*input data;
DATA seben;
INPUT Planted Survived PestControl Fertil Precip WindSpeed;
DATALINES;
125 125 3 1 18 9.6
115 68 0 0 8 13.4
250 101 1 1 17 12.8
95 85 2 2 22 10
140 48 3 1 15 15.1
75 75 3 2 27 6.3
185 163 3 3 15 12.3
20 9 3 0 18 9.4
110 83 3 1 24 13.1
80 80 0 1 18 7.8
120 117 4 1 20 9.3
90 56 5 1 15 13.9
30 30 3 0 33 8.6
90 81 4 1 23 7.7
140 119 3 1 18 11.8
70 9 3 0 32 8.4
75 71 3 3 20 13.4
150 102 5 0 16 9.7
90 73 4 1 15 9.7
160 151 6 1 18 7.8
100 46 3 1 20 12.3
85 85 4 1 22 6.8
120 85 2 1 19 6.6
180 53 3 1 29 9.4
45 12 0 1 9 13.1
35 35 1 0 7 12.4
;
RUN;

*normalize;
DATA seben;
SET seben;
Survival = Survived / Planted;
RUN;

%LET response= Survival;

*fit the null model;
*betas predict one;
*gammas predict the non-one part;
PROC NLMIXED DATA= seben;
PARMS 
b0=0.1 g0=0.1 phi=0.1;
pi1 = exp(b0) / (1 + exp(b0));
mu = exp(g0) / (1 + exp(g0));
IF (&response = 1)
THEN loglikelihood = log(pi1);
ELSE loglikelihood = log(1-pi1) + lgamma(phi) - lgamma(mu*phi)
- lgamma((1-mu)*phi) + (mu*phi-1)*log(&response)
+ ((1-mu)*phi-1)*log(1-&response);
MODEL &response ∼ GENERAL(loglikelihood);
RUN;

*fit the full model with all predictors;
*betas predict one, choosing precip and windspeed;
*gammas predict the non-one part, choosing the rest;
PROC NLMIXED DATA= seben;
PARMS
b0=0.1 b1=0.1 b2=0.1
g0=0.1 g1=0.1 g2=0.1
phi=0.1;
pi1 = exp(b0 + b1*Precip + b2*WindSpeed) / (1 + exp(b0 + b1*Precip + b2*WindSpeed));
mu = exp(g0 + g1*PestControl + g2*Fertil) / (1 + exp(g0 + g1*PestControl + g2*Fertil));
IF (&response = 1) 
THEN loglikelihood = log(pi1);
ELSE loglikelihood = log(1-pi1) + lgamma(phi) - lgamma(mu*phi)
- lgamma((1-mu)*phi) + (mu*phi-1)*log(&response)
+ ((1-mu)*phi-1)*log(1-&response);
MODEL &response ∼ GENERAL(loglikelihood);
RUN;

DATA dev;
deviance = 22.2 - 6.9;
pval = 1 - probchi(deviance, 4);
RUN;
PROC PRINT DATA= dev; RUN;
