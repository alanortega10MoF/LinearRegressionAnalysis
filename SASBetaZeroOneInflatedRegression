*input data;
DATA kyuu;
INPUT EleCond Temp Alt GermRate @@;
DATALINES;
2.7 67 4368 0.00 2.5 64 4229 0.17
1.1 67 1689 1.00 1.8 69 2933 0.47
1.8 69 3156 0.87 1.8 63 6110 0.32
1.6 67 4884 0.58 1.5 67 461 1.00
2.4 66 4926 0.00 2.5 67 5269 0.00
1.6 63 3854 0.23 1.7 74 197 1.00
2.3 67 5146 0.00 1.6 65 607 1.00
1.2 64 2202 0.48 2.6 67 5263 0.16
1.1 62 2759 0.82 1.2 69 651 1.00
1.9 62 2774 0.61 1.7 65 863 0.80
1.5 71 5927 0.19 1.5 62 4386 0.23
1.7 61 827 0.93 1.7 68 165 1.00
2.8 62 3631 0.00 1.7 62 234 0.73
;
RUN;

*normalize;
DATA kyuu;
SET kyuu;
Alt = Alt / 1000;
RUN;

%LET response= GermRate;

*fit the null model;
*betas predict the middle;
*gammas predict nu, nu mostly influences pi0;
*zetas predict tau, tau mostly influences pi1;
PROC NLMIXED DATA= kyuu;
PARMS 
b0=0.1 
g0=0.1 
z0=0.1 
phi=0.1;
mu = exp(b0) / (1 + exp(b0));
nu = exp(g0);
tau = exp(z0);
pi0=nu/(1+nu+tau);
pi1=tau/(1+nu+tau);
IF (&response =0) THEN loglikelihood = log(pi0);
IF (&response =1) THEN loglikelihood = log(pi1);
IF (&response > 0 AND &response < 1) 
THEN loglikelihood = log(1-pi0-pi1)+lgamma(phi)-lgamma(mu*phi)
-lgamma((1-mu)*phi)+(mu*phi-1)*log(&response)
+((1-mu)*phi-1)*log(1-&response);
MODEL &response ∼ GENERAL(loglikelihood);
RUN;

*fit the full model with all predictors;
*betas predict the middle;
*gammas predict nu, nu mostly influences pi0;
*zetas predict tau, tau mostly influences pi1;
PROC NLMIXED DATA= kyuu;
PARMS 
b0=0.1 b1=0.1
g0=0.1 g1=0.1
z0=0.1 z1=0.1
phi=0.1;
mu = exp(b0 + Alt*b1) / (1 + exp(b0 + Alt*b1));
nu = exp(g0 + EleCond*g1);
tau = exp(z0 + Temp*z1);
pi0=nu/(1+nu+tau);
pi1=tau/(1+nu+tau);
IF (&response =0) THEN loglikelihood = log(pi0);
IF (&response =1) THEN loglikelihood = log(pi1);
IF (&response > 0 AND &response < 1) 
THEN loglikelihood = log(1-pi0-pi1)+lgamma(phi)-lgamma(mu*phi)
-lgamma((1-mu)*phi)+(mu*phi-1)*log(&response)
+((1-mu)*phi-1)*log(1-&response);
MODEL &response ∼ GENERAL(loglikelihood);
RUN;

DATA dev;
deviance = 49.2 - 17.6;
pval = 1 - probchi(deviance, 3);
RUN;
PROC PRINT DATA= dev; RUN;
